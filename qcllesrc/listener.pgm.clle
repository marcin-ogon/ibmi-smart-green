PGM PARM(&LIB)

DCL VAR(&LIB)    TYPE(*CHAR) LEN(10)
DCL VAR(&REQ)    TYPE(*CHAR) LEN(10)   VALUE('REQUEST')
DCL VAR(&RSP)    TYPE(*CHAR) LEN(10)   VALUE('RESPONSE')
DCL VAR(&MSG)    TYPE(*CHAR) LEN(100)
DCL VAR(&MSGLEN) TYPE(*DEC)  LEN(5 0)  VALUE(100)
DCL VAR(&WAIT)   TYPE(*DEC)  LEN(5 0)  VALUE(-1)

DCL VAR(&VAR) TYPE(*CHAR) LEN(148) 
DCL VAR(&VARLEN) TYPE(*CHAR) LEN(4) VALUE(x'00000094')
DCL VAR(&FORMAT) TYPE(*CHAR) LEN(8) VALUE('SSTS0200')
DCL VAR(&RESET) TYPE(*CHAR) LEN(10) VALUE('*NO')
DCL VAR(&ERROR) TYPE(*CHAR) LEN(4) VALUE(X'00000000')

DCL VAR(&DATA_STRCT) TYPE(*CHAR) LEN(15)
DCL VAR(&UNITU)  TYPE(*CHAR)  STG(*DEFINED) LEN(5) DEFVAR(&DATA_STRCT 1) /* % processing unit used */
DCL VAR(&INTPRF) TYPE(*CHAR)  STG(*DEFINED) LEN(5) DEFVAR(&DATA_STRCT 6) /* % current interactive performance */
DCL VAR(&DBCAP)  TYPE(*CHAR)  STG(*DEFINED) LEN(5) DEFVAR(&DATA_STRCT 11) /* % DB capability */

CHKOBJ OBJ(&LIB/&REQ) OBJTYPE(*DTAQ)
MONMSG MSGID(CPF9801) EXEC(CRTDTAQ DTAQ(&LIB/&REQ) MAXLEN(&MSGLEN)) 

CHKOBJ OBJ(&LIB/&RSP) OBJTYPE(*DTAQ)
MONMSG MSGID(CPF9801) EXEC(CRTDTAQ DTAQ(&LIB/&RSP) MAXLEN(&MSGLEN))

    DOWHILE (1 = 1)
        CALL PGM(QRCVDTAQ) PARM(&REQ &LIB &MSGLEN &MSG &WAIT)
        IF COND(&MSG *NE ' ') THEN(DO)

            CALL PGM(QWCRSSTS) PARM(&VAR &VARLEN &FORMAT &RESET &ERROR)
            CHGVAR VAR(&UNITU)  VALUE(%BIN(&VAR 33 4))
            CHGVAR VAR(&INTPRF) VALUE(%BIN(&VAR 129 4))
            CHGVAR VAR(&DBCAP)  VALUE(%BIN(&VAR 69 4))

            CHGVAR VAR(&MSG)  VALUE(&DATA_STRCT)
            CHGVAR VAR(&MSGLEN)  VALUE(%LEN(&DATA_STRCT))
            CALL PGM(QSNDDTAQ) PARM(&RSP &LIB &MSGLEN &DATA_STRCT)
        ENDDO
    ENDDO

END: ENDPGM